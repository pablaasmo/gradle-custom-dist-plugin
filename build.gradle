import org.gradle.api.publish.maven.MavenPublication

plugins { id 'com.jfrog.bintray' version '0.5' }

apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'net.praqma.gradle'
version = '0.3'
description = 'Gradle plugin to create a customized Gradle distribution'

ext {
	pluginId = 'net.praqma.customgradledistribution'
	artifact = 'custom-gradle-dist-plugin'
}

assert file("src/main/resources/META-INF/gradle-plugins/${pluginId}.properties").exists()

dependencies {
	compile gradleApi()
	compile localGroovy()
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId artifact

			from components.java
			artifact sourceJar

			pom.withXml {
				asNode().appendNode('description', project.description)
			}
		}
	}
}

bintray {
	user = "${bintrayUser}"
	key = "${bintrayApiKey}"
	publications = ['mavenJava']
	dryRun = false //Whether to run this as dry-run, without deploying
	publish = true //If version should be auto published after an upload
	pkg {
		repo = 'gradle-plugins'
		userOrg = 'praqma'
		name = pluginId
		desc = project.description
		websiteUrl = 'https://github.com/Praqma/gradle-custom-dist-plugin'
		vcsUrl = 'https://github.com/Praqma/gradle-custom-dist-plugin.git'
		licenses = ['Apache-2.0']
		publicDownloadNumbers = true
		//Optional version descriptor
		version {
			attributes = ['gradle-plugin': "${pluginId}:${group}:${artifact}"]
		}
	}
}
